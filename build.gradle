plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'maven-publish'
}

version = "${mod_version}+${minecraft_version}-Fabric"
group = project.mod_group_id

base {
	archivesName = mod_comp_name
}

repositories {
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}
	maven { url "https://maven.architectury.dev/" }
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}


loom {
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${project.p_mc_version}:${project.parchment_date}@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// MCMV
	modCompileOnly  "curse.maven:more-composter-variants-1058421:${project.mcmv_version_id}"

	// AFT
	modApi "dev.architectury:architectury-fabric:${project.architectury_api_version}"
	modCompileOnly "curse.maven:animal-feeding-trough-445838:${project.aft_version_id}"
}

processResources {
	var replaceProperties = [
			loader_version: loader_version, mod_id: mod_id, mod_name: mod_name,
			mod_license: mod_license, mod_version: mod_version, mod_authors: mod_authors,
			mod_description: mod_description, quad_version: quad_version
	]
	inputs.properties replaceProperties

	filesMatching("fabric.mod.json") {
		expand replaceProperties
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {}
}